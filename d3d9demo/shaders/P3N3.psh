float4 g_gnd; // ground hemisphere light intensity
float4 g_sky; // sky hemisphere light intensity
float4 g_a; // material ambient color
float4 g_d; // light intensity * material diff color
float4 g_s; // light intensity * material spec color and material shininess
float3 g_u; // world up in model space

struct PS_INPUT {
	float3 n : TEXCOORD0; // model space interpolated vertex normal + hemisphere scalar
	float3 v : TEXCOORD1; // model space interpolated vertex to camera vector
	float3 l : TEXCOORD2; // model space interpolated vertex to omniLS vector
	float a : TEXCOORD3; // interpolated attenuation
};

void main(PS_INPUT ps_input, out float4 c : COLOR) {
	float3 n = normalize(ps_input.n);
	float3 v = normalize(ps_input.v);
	float3 l = normalize(ps_input.l);

	float n_dot_l = saturate(dot(n, l));
	float v_dot_r = saturate(dot(v, 2.f * n_dot_l * n - l));
	float self_shadow = saturate(4.f * n_dot_l);

	float4 ambient = g_a * lerp(g_gnd, g_sky, dot(n, g_u) * .5f + .5f);
	float4 diffuse = g_d * n_dot_l;

	c = ambient + self_shadow * ps_input.a * (diffuse + g_s * pow(v_dot_r, g_s.a));
	c.a = 1.0f;
}
