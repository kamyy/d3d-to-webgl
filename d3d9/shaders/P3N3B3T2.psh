float4 g_gnd; // ground hemisphere light intensity
float4 g_sky; // sky hemisphere light intensity
float4 g_i; // light intensity
float4 g_s; // light intensity * material spec color and material shininess

sampler s0; // diffuse map
sampler s1; // normal map

struct PS_INPUT {
	float3 v : TEXCOORD0; // interpolated tangent space vertex to camera vector
	float3 l : TEXCOORD1; // interpolated tangent space vertex to spotLS vector
	float3 u : TEXCOORD2; // interpolated tangent space world up vector
	float2 t : TEXCOORD3; // interpolated texture coordintes
	float a : TEXCOORD4; // interpolated attenuation
};

void main(PS_INPUT ps_input, out float4 c : COLOR) {
	float3 v = normalize(ps_input.v);
	float3 l = normalize(ps_input.l);
	float3 u = normalize(ps_input.u);
	float4 d = tex2D(s0, ps_input.t); // diffuse map
	float3 n = normalize(tex2D(s1, ps_input.t) * 2.f - 1.f); // normal map

	float n_dot_l = saturate(dot(n, l));
	float v_dot_r = saturate(dot(v, 2.f * n_dot_l * n - l));
	float self_shadow = saturate(4.f * l.z);

	float4 ambient = d * lerp(g_gnd, g_sky, dot(n, u) * .5f + .5f);
	float4 diffuse = d * g_i * n_dot_l;

	c = ambient + self_shadow * ps_input.a * (diffuse + g_s * pow(v_dot_r, g_s.a));
	c.a = d.a;
}
